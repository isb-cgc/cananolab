# caNanoLab Wildfly setup scripts

connect localhost:@client.port@

##############################################################
#             System Properties                      
##############################################################

if (outcome == success) of /system-property=gov.nih.nci.security.configFile:read-resource
    /system-property=gov.nih.nci.security.configFile:remove
end-if

if (outcome == success) of /system-property=gov.nih.nci.cananolab.wikihelpProperties:read-resource
	/system-property=gov.nih.nci.cananolab.wikihelpProperties:remove
end-if

/system-property=gov.nih.nci.cananolab.wikihelpProperties:add(value=@file.repository.dir@/config/wikihelp.properties)


##############################################################
#             Datasource Configuration                      
##############################################################

if (outcome != success) of /subsystem=datasources/jdbc-driver=mysql:read-resource
    /subsystem=datasources/jdbc-driver=mysql:add( \
    driver-name=mysql, \
    driver-module-name=com.mysql, \
    driver-xa-datasource-class-name=com.mysql.jdbc.Driver \
	) {allow-resource-service-restart=true}
end-if

if (outcome == success) of /subsystem=datasources/data-source=caNanoLab:read-resource
  data-source remove --name=caNanoLab
end-if

#caNanoLab datasource configuration
if (outcome != success) of /subsystem=datasources/data-source=caNanoLab:read-resource
data-source add \
    --name=caNanoLab \
    --driver-name=mysql \
    --connection-url=@database.url@ \
    --jndi-name=java:/caNanoLab \
    --user-name=@database.user@ \
    --password=@database.password@ \
    --use-ccm=true \
    --max-pool-size=100 \
    --blocking-timeout-wait-millis=5000
end-if

# Unload the welcome page
/subsystem=undertow/server=default-server/host=default-host/location=\/:remove()

########################################################################
#   LOGGING
######################################################################

# Adjust the console handler
/subsystem=logging:write-attribute(name=use-deployment-logging-config, value=false)
/subsystem=logging:write-attribute(name=add-logging-api-dependencies,value=true)
/subsystem=logging/console-handler=CONSOLE:write-attribute(name=autoflush, value=true)
/subsystem=logging/console-handler=CONSOLE:write-attribute(name=level, value=INFO)
/subsystem=logging/console-handler=CONSOLE:write-attribute(name="formatter", value="%d [%t] %-5p [%c : %L] %m%n")

# Point stdout and stderr to it
/subsystem=logging/logger=stderr:add(use-parent-handlers="false", handlers=[CONSOLE])
/subsystem=logging/logger=stdout:add(use-parent-handlers="false", handlers=[CONSOLE])

# Pull the root logger off the File handler; file logging is useless in AppEngine
/subsystem=logging/root-logger=ROOT:remove-handler(name=FILE)
/subsystem=logging/root-logger=ROOT:write-attribute(name=level, value=DEBUG)

# Hook up our cananolab code to the console handler
/subsystem=logging/logger=gov.nih.nci.cananolab:add(handlers=["CONSOLE"])

:reload

